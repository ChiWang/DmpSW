#=====================================================================
#   File:   Simulation.scons
#   Author: Chi WANG  (chiwang@mail.ustc.edu.cn)    22/01/2014
#---------------------------------------------------------------------
#   Description:
#       This file is invoked by ../SConstruct
#
#---------------------------------------------------------------------
#   History:
#                           Last update:  22/01/2014   23:14:28
#=====================================================================

import os
EnvSim = Environment(ENV = os.environ)

#   set the programme name
target='dmpSim'

#   set debug modle.
#    if yes, then can use gdb:  http://www.ibm.com/developerworks/cn/linux/sdk/gdb/
if os.environ.has_key('DMPOSSYS'):
    print "\t\tdebug mode\t re-install Simulation\n"
    installDir=os.environ['DMPOSSYS']
    EnvSim.Prepend(CCFLAGS = '-g')
    EnvSim.Append(CCFLAGS = '-DDmp_DEBUG')
else:
    Import('prefix')
    installDir=prefix
    EnvSim.Append(CCFLAGS = '-DDmp_RELEASE')

#   set Geant4 environment
if int(ARGUMENTS.get('g4',1)):
    g4sys=os.environ['G4INSTALL']
    EnvSim.PrependENVPath('PATH', g4sys+'/../../../bin')
    EnvSim.ParseConfig("geant4-config --cflags --libs")

#   set Root environment
if int(ARGUMENTS.get('root',1)):
    EnvSim.ParseConfig("root-config --cflags --libs")

#   set OpenMP
if int(ARGUMENTS.get('omp',0)):
    target=target+"_omp"
    EnvSim.MergeFlags('-fopenmp')

#   include
for key in ['DmpSim','Psd','Stk','Bgo','Nud']:
    includeDir=key+'/include'
    EnvSim.Prepend(CPPPATH=[includeDir])

#   Use other top-1 package
    #+  Kernel
P1Path='../Kernel'
EnvSim.Prepend(CPPPATH=[P1Path+'/include'])
    #+  Event library
P2Path='../Event/'
for key in ['DmpEvent','Psd','Stk','Bgo','Nud']:
    includeDir=P2Path+key+'/include'
    EnvSim.Prepend(CPPPATH=[includeDir])

EnvSim.Prepend(LIBS=['DmpCore','DmpEvent'])
EnvSim.Prepend(LIBPATH=[P1Path,P2Path])

#   install share
shareDir=installDir+"/share/connector"
if not os.path.exists(shareDir):
    import shutil
    shutil.copytree("./share/connector",shareDir)

#   main build
goal=EnvSim.Program(target,Glob('./*/src/*.cc'))
Default(EnvSim.InstallAs(installDir+'/bin/'+target,goal))


