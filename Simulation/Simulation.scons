'''
 *  $Id: Simulation.scons, 2014-03-07 15:24:16 chi $
 *  Author(s):
 *    Chi WANG (chiwang@mail.ustc.edu.cn) 22/01/2014
'''

import os
Import("*")
print "\t\t\t invoke %s\n" % os.path.abspath("./")
#--------------------------------------------------------------------

target='DmpSimulation'
env = envBase.Clone()
useRoot(env)
useGeant4(env)
env.Prepend(CPPFLAGS = '-DG4VIS_USE')
env.Append(CPPFLAGS = ' -DG4UI_USE')
env.Append(CPPFLAGS = ' -DG4VIS_USE_OPENGLX')
useBoostPython(env)
#useCLHEP(env)
#useOpenMP(env)

# include
env.Prepend(CPPPATH='DmpSim/include')
for key in subDetectors:
    includeDir=key+'/include'
    env.Prepend(CPPPATH=[includeDir])

# Use other top-1 package
#--------------------------------------------------------------------
    #+  Kernel
P1Path='../Kernel'
env.Prepend(CPPPATH=[P1Path+'/include'])
    #+  Event
P2Path='../Event'
env.Prepend(CPPPATH=P2Path+'/DmpEvent/include')
for key in subDetectors:
    includeDir=P2Path+"/"+key+'/include'
    env.Prepend(CPPPATH=[includeDir])

env.Prepend(LIBS=['DmpCore','DmpEvent','EG'])   # EG is for TParticle
env.Prepend(LIBPATH=[P1Path,P2Path])

# install share
#--------------------------------------------------------------------
for file in os.listdir('./share'):
    if "." not in file[0] and "JobOpt" in file:
        Default(Command(prefix+'/share/TestRelease/'+file,'./share/'+file,Copy("$TARGET","$SOURCE")))
    if "." not in file[0] and ".mac" in file and "DmpSim" in file:
        Default(Command(prefix+'/share/Simulation/'+file,'./share/'+file,Copy("$TARGET","$SOURCE")))

# main build
#--------------------------------------------------------------------
#goal=env.Program(target,Glob('./*/src/*.cc'))
#Default(env.InstallAs(prefix+'/bin/'+target,goal))
goal=env.SharedLibrary(target,Glob('./*/src/*.cc'))
Default(env.InstallAs(prefix+"/lib/lib"+target+".so",goal))


