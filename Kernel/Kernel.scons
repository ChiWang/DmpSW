#=====================================================================
#   File:   SConstruct
#   Author: Chi WANG  (chiwang@mail.ustc.edu.cn)    24/07/2013
#---------------------------------------------------------------------
#   Description:
#
#   1.  Root
#       ARGUMENTS.get('root',value):
#           value = 1,      use root
#           scons root=0,   not use
#   2.  Geant4
#         ARGUMENTS.get('g4',value):
#           value = 0,      not use geant4
#           scons g4=1,     use geant4
#   3.  OpenMP
#         ARGUMENTS.get('omp',value):
#           value = 0,      not use OpenMP
#           scons omp=1,    use it
#
#---------------------------------------------------------------------
#   History:
#                           Last update:  13/12/2013   12:25:44
#=====================================================================

import os
EnvKernel = Environment(ENV = os.environ)

#   set the programme name
LibName='DmpCore'

#   set debug modle.
#    if yes, then can use gdb:  http://www.ibm.com/developerworks/cn/linux/sdk/gdb/
if os.environ.has_key('DMPOSSYS'):
    print "\t\tdebug mode\t re-install Kernel\n"
    installDir=os.environ['DMPOSSYS']
    EnvKernel.Prepend(CCFLAGS = '-g')
    EnvKernel.Append(CCFLAGS = '-DDmp_DEBUG')
else:
    Import('prefix')
    installDir=prefix
    EnvKernel.Append(CCFLAGS = '-DDmp_RELEASE')

#   set Geant4 environment
if int(ARGUMENTS.get('g4',0)):
    g4sys=os.environ['G4INSTALL']
    EnvKernel.PrependENVPath('PATH', g4sys+'/../../../bin')
    EnvKernel.ParseConfig("geant4-config --cflags --libs")

#   set Root environment
if int(ARGUMENTS.get('root',1)):
    EnvKernel.ParseConfig("root-config --cflags --libs")

#   set OpenMP
if int(ARGUMENTS.get('omp',0)):
    LibName=LibName+"_omp"
    EnvKernel.MergeFlags('-fopenmp')

#   include
includeDir='./include'
EnvKernel.Prepend(CPPPATH=[includeDir])
for file in os.listdir(includeDir):
    if "LinkDef" not in file and "Dictionary" not in file:
        Default(Command(installDir+'/include/'+file,includeDir+'/'+file,Copy("$TARGET","$SOURCE")))

#   install script
for file in os.listdir('./script'):
    if "Readme" not in file:
        Default(Command(installDir+'/bin/'+file,'./script/'+file,Copy("$TARGET","$SOURCE")))

#   main build
execute=EnvKernel.SharedLibrary(LibName,Glob('./src/*.cc'))
Default(EnvKernel.InstallAs(installDir+'/lib/lib'+LibName+".so",execute))


