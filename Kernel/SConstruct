#=====================================================================
#   File:   SConstruct
#   Author: Chi WANG  (chiwang@mail.ustc.edu.cn)    24/07/2013
#---------------------------------------------------------------------
#   Description:
#
#   *** Default degub=0, scons debug=1 will create in debug mode
#         means: create all thing in current directory instead of install into /prefix
#
#   1.  Root
#       ARGUMENTS.get('root',value):
#           value = 1,      use root
#           scons root=0,   not use
#   2.  Geant4
#         ARGUMENTS.get('g4',value):
#           value = 0,      not use geant4
#           scons g4=1,     use geant4
#   3.  OpenMP
#         ARGUMENTS.get('omp',value):
#           value = 0,      not use OpenMP
#           scons omp=1,    use it
#
#---------------------------------------------------------------------
#   History:
#                           Last update:  13/12/2013   12:25:44
#=====================================================================

print "\n\t\t--->building DAMPE Software Kernel"

import os
EnvKernel = Environment(ENV = os.environ)

#   set the programme name
LibName='DmpCore'

#   set debug modle.
#    if yes, then can use gdb:  http://www.ibm.com/developerworks/cn/linux/sdk/gdb/
debug=ARGUMENTS.get('debug',1)
if int(debug):
    print "\t\t debug mode"
    EnvKernel.Prepend(CCFLAGS = '-g')
    EnvKernel.Append(CCFLAGS = '-DDmp_DEBUG')
    installdir=os.environ['DMPSWWORK']
else:
    EnvKernel.Append(CCFLAGS = '-DDmp_RELEASE')
    Import('PREFIX')
    installdir=PREFIX

#   set Geant4 environment
if int(ARGUMENTS.get('g4',0)):
    g4sys=os.environ['G4INSTALL']
    EnvKernel.PrependENVPath('PATH', g4sys+'/../../../bin')
    EnvKernel.ParseConfig("geant4-config --cflags --libs")

#   set Root environment
if int(ARGUMENTS.get('root',1)):
    EnvKernel.ParseConfig("root-config --cflags --libs")

#   set OpenMP
if int(ARGUMENTS.get('omp',0)):
    LibName=LibName+"_omp"
    EnvKernel.MergeFlags('-fopenmp')

#   include
includeDir='./include'
if int( not debug):
    for file in os.listdir(includeDir):
        Default(Command(PREFIX+'/include/'+file,includeDir+'/'+file,Copy("$TARGET","$SOURCE")))
EnvKernel.Prepend(CPPPATH=[includeDir])

#   main build
execute=EnvKernel.SharedLibrary(LibName,Glob('./src/*.cc'))
Default(EnvKernel.InstallAs(installdir+'/lib/lib'+LibName+".so",execute))


